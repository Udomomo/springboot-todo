plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'org.openapi.generator' version '4.2.3'
}

group = 'com.udomomo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

openApiValidate {
	inputSpec = "${rootDir}/spec/openapi.yaml"
}

openApiGenerate {
	generatorName = "spring"
	configFile = "${rootDir}/spec/config.json"
	inputSpec = "${rootDir}/spec/openapi.yaml"
	outputDir = "${buildDir}/generated"
	configOptions = [
			dateLibrary: "java8"
	]
	systemProperties = [
			modelDocs: 'false'
	]
}

compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate
sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}
